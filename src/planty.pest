
program = _{ SOI ~ "\n"* ~ "@startuml" ~ "\n"+ ~ (stmt ~ "\n"+)* ~ "@enduml" ~ "\n"* ~ EOI }

stmt = {
      participant
    | message
    | group_start
    | group_end
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

participant = { participant_type ~ ( ident | string ) ~ participant_alias? }
participant_type = { "participant" | "actor" | "database" }
participant_alias = { "as" ~ ident }

arrow = { "->" | "-->" | "<--" | "<-" }
label = { (!"\n" ~ ANY)* }
message_label = { ":" ~ label }
message = { ident ~ arrow ~ ident ~ message_label? }

group_start = { group_type ~ label }
group_type = { "group" | "alt" }

group_end = { "end" }

WHITESPACE = _{ " " | "\t" }
