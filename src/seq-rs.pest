
program = _{ SOI ~ "\n"* ~ "@startuml" ~ "\n"+ ~ (stmt ~ "\n"+)* ~ "@enduml" ~ "\n"* ~ EOI }

stmt = {
      participant
    | message
    | group_start
    | group_end
    | alt_else
    | activate
    | deactivate
    | message_note
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

participant = { participant_type ~ ( ident | string ) ~ participant_alias? }
participant_type = { "participant" | "actor" | "database" }
participant_alias = { "as" ~ ident }

arrow = { "->" | "-->" | "<--" | "<-" }
label = { (!"\n" ~ ANY)* }
message_label = { ":" ~ label }
message = { ident ~ arrow ~ ident ~ activation_modifier? ~ message_label? }
activation_modifier = { "++" | "--" }

group_start = { group_type ~ label }
alt_else = { "else" ~ label }
group_type = { "group" | "alt" }
group_end = { "end" }

activate = { "activate" ~ ident }
deactivate = { "deactivate" ~ ident }

message_note = { "note" ~ note_orientation ~ ( note_label | note_multiline_label ) }
note_label = { ":" ~ label }
note_multiline_label = { "\n" ~ multiline_label ~ "\n" ~ "end note" }
multiline_label = { ( !("\n" ~ "end note") ~ ANY )* }
note_orientation = { note_left | note_right | note_over }
note_left = { "left" }
note_right = { "right" }
note_over = { "over" ~ (ident ~ ",")* ~ ident }

WHITESPACE = _{ " " | "\t" }
